buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def keystorePropertiesFile = rootProject.file("local.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.awolity.trakr"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        vectorDrawables.useSupportLibrary = true
        // multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        lintOptions {
            checkReleaseBuilds false
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "featureset"
    productFlavors {
        free {
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
            dimension "featureset"
        }
        pro {
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"
            dimension "featureset"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':TrakrViews')
    implementation project(path: ':TrakrUtils')
    implementation "com.android.support:customtabs:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"

    // maps
    implementation "com.google.android.gms:play-services-maps:${playServicesVersion}"

    // location
    implementation "com.google.android.gms:play-services-location:${playServicesVersion}"

    //ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:${androidArchVersion}"

    // room
    implementation "android.arch.persistence.room:runtime:${androidArchVersion}"
    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    annotationProcessor "android.arch.persistence.room:compiler:${androidArchVersion}"

    // dagger 2
    implementation 'com.google.dagger:dagger-android:2.15'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'

    // firebase
    implementation "com.google.firebase:firebase-core:16.0.0"

    // crashlitycs
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'

    // firebase realtime database
    implementation "com.google.firebase:firebase-database:16.0.1"

    // firebase authentication
    implementation "com.google.firebase:firebase-auth:16.0.1"

    // firebase UI for firebase auth
    implementation 'com.firebaseui:firebase-ui-auth:3.3.1'

    // MPAndroid Chart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'

    // gpx parser
    implementation 'com.codebutchery.android:gpx_lib:1.0.3'

    // switch
    implementation 'lib.kingja.switchbutton:switchbutton:1.1.7'

    // about libraries
    implementation 'com.mikepenz:aboutlibraries:6.0.0'

    // admob
    freeImplementation "com.google.android.gms:play-services-ads:${adsVersion}"
    freeImplementation "com.google.firebase:firebase-ads:${adsVersion}"

    // instabug
    implementation 'com.instabug.library:instabug:8.0.5'
}

apply plugin: 'com.google.gms.google-services'
